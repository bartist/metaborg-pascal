module desugar

imports
  signatures/-

strategies

  desugar-all = topdown(try(desugar))
  
rules

  desugar: 
  	Some(thing) -> thing

  desugar:
  	Label(stringnum) -> Label(intnum)
  	where <string-to-int> stringnum => intnum
  	
  desugar:
    Goto(Label(stringnum)) -> Goto(intnum)
  	where <string-to-int> stringnum => intnum
  
  desugar:
    Labeled(Label(stringnum), stmnt) -> Labeled(intnum, stmnt)	
  	where <string-to-int> stringnum => intnum
  
  desugar:
    ArrayType([range_outer | range_inner], type) -> ArrayType(range_outer, sub_type)
    where
    	<desugar>ArrayType(range_inner,type) => sub_type
  
  desugar:
  	ArrayType([], type) -> type
  	
  desugar: 
  	IndexVar(ref, [position | positions]) -> IndexVar(inner, position)
  	where
  		<desugar>IndexVar(ref, positions) => inner
  		
  desugar:
  	IndexVar(ref, []) -> ref
  
  desugar :
    Params(ps) -> Params(ps')
    where <mapconcat(try(desugar-param))> ps => ps'
    
  desugar-param :
    VarParams(xs, t) -> <map(!VarParams([<id>], t))> xs
    
  desugar-param :
    ValueParams(xs, t) -> <map(!ValueParams([<id>], t))> xs

  // Transform procedures to Void function calls
  
  desugar :
  	ProcCall(fun, params) -> Assign(NilE(), FunCall(fun, params))
  
  desugar :
  	ProcDecl(h,b) -> FuncDecl(h', b)
  	where <desugar-proc-heading> h => h'

  desugar-proc-heading :
  	ProcHeading(name, params) -> FuncHeading(name, params, TypeId("_void"))
  
  // Remove quotationmarks from string
  
  desugar :
  	String(str) -> String(str'')
  	with
  		 <un-single-quote> str => str';
  		 <string-replace(|"''","'")> str' => str''

  desugar : 
    Char(char) -> Char(char')
    where
    	<un-single-quote> char => char'
  	
//  desugar : 
//    TypeId(str) -> TypeId(<lower-case>str)
//  
//  desugar :
//    TypeDef(x, t) -> TypeDef(<lower-case>x, t)